buildscript {
	ext {
		springBootVersion = '2.0.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id "org.hidetake.ssh" version "2.9.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'homework'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
// compileJava.options.compilerArgs += ["-proc:none"]

eclipseJdt.doLast {
	File f = file('.settings/org.eclipse.core.resources.prefs')
	f.write('eclipse.preferences.version=1\n')
	f.append('encoding/<project>=UTF-8')
}

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-jersey')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-mail')
	compile('org.liquibase:liquibase-core')
	compile('org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.2')
	compile('io.jsonwebtoken:jjwt:0.9.0')
	compile('com.google.guava:guava:25.0-jre')
	compile('org.apache.commons:commons-math3:3.6.1')

	compile('builders.dsl:spreadsheet-builder-api:1.0')
	compile('builders.dsl:spreadsheet-builder-poi:1.0')
	//compile('org.apache.poi:poi:3.17')

	compile group: 'space.jasan', name: 'groovy-closure-support', version: '0.2.0'

	runtime('org.postgresql:postgresql')
	compileOnly('org.projectlombok:lombok')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	// testCompile('org.hsqldb:hsqldb:2.4.1')
	testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

test {
	beforeTest { descriptor ->
		logger.lifecycle("Running test: " + descriptor)
	}
	onOutput { descriptor, event ->
		logger.lifecycle(event.message.minus(System.lineSeparator()))
	}
}

war {
	enabled = true
	archiveName = 'ROOT.war'

	dependsOn ':dia-client:buildProd'

	into('WEB-INF/classes/static') {
		from "${project(':homework-client').projectDir}/dist/homework-client"
	}
}
